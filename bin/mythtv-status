#!/usr/bin/perl -w
# Copyright (c) 2007 Andrew Ruthven <andrew@etc.gen.nz>
# This code is hereby licensed for public consumption under the GNU GPL v2.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA

# Display the current status of a MythTV system.

use LWP::Simple;
use XML::LibXML;
use Date::Manip;
use Getopt::Long;
use Text::Wrap;

# Some sane defaults.
my $host = "localhost";
my $port = "6544";
my $description = undef;
my $episode = undef;

GetOptions(
  'd|descripton' => \$description,
  'e|episode'    => \$episode,
  'h|host=s'     => \$host,
  'p|port=s'     => \$port,
);

die "Sorry, port isn't a number.\n"
  if $port !~ /^\d+$/;

my $url = "http://$host:$port/xml";
my $status = get($url);

die "Sorry, failed to fetch $url.\n"
  unless defined $status;

my $parser = XML::LibXML->new();
my $xml = eval { $parser->parse_string( $status ) };

our $today    = substr(ParseDate('today'), 0, 8);
our $tomorrow = substr(ParseDate('tomorrow'), 0, 8);

# The blocks of output which we might generate.
my @blocks = (
  # Date/Time from server
  {
    'name'  => 'Status',
    'xpath' => "//Status",
    'attrs' => [ qw/time date/ ],
    'template' => "__date__, __time__",
    'format' => 'one line'
  },

  # Info about the encoders.
  {
    'name'  => 'Encoders',
    'xpath' => "//Status/Encoders/Encoder",
    'attrs' => [ qw/hostname id state connected/ ],
    'template' => "__hostname__ (__id__) - __state____connected__",
    'rewrite' => {
      'connected' => { '1' => '', '0' => ' (Disconnected)' },
      'state' =>{ '0' => 'Idle',
                  '1' => 'Watching LiveTV',
                  '2' => 'Watching Pre-recorded',
		  '3' => 'Watching Recording',
		  '4' => 'Recording' },
    }
  },

  # What programs (if any) are being recorded right now?
  {
    'name'  => 'Recording Now',
    'xpath' => "//Status/Encoders/Encoder/Program",
    'attrs' => [ qw/title endTime/ ],
    'template' => "__title__ (Ends: __endTime__)",
     'rewrite' => {
       'endTime' => { 'T' => ' ' },
     }
  },

  # The upcoming recordings.
  {
    'name'  => 'Scheduled Recordings',
    'xpath' => '//Status/Scheduled/Program',
    'attrs' => [ qw/title startTime NODE_TEXT subTitle/ ],
    'template' => "__startTime__ - __title__" 
      . (defined $episode ? " - __subTitle__" : '')
      . (defined $description ? "\n__NODE_TEXT__" : ''),
    'filter' =>  {
       # Only show recordings for today and tomorrow.
       'startTime' => sub {
	   my $date = substr(ParseDate($_[0]), 0, 8);
           return ! (($date cmp $today) == 0
	      || ($date cmp $tomorrow) == 0) }
     },
     'rewrite' => {
       'startTime' => { 'T' => ' ' },
     }
  }
);

my $title =  "MythTV status for $host";
print "\n$title\n";
print '=' x length($title) . "\n";

for my $block (@blocks) {
  my $items = $xml->documentElement->find($block->{'xpath'});

  # Don't do any work on this block if there is nothing for it.
  next
    if (scalar(@$items) == 0);

  $block->{'format'} ||= 'multi line';
  print "$block->{'name'}:" . ($block->{'format'} eq 'one line' ? ' ' : "\n");

  for my $item (@{ $items }) {
    my $template = $block->{'template'};
    my $skip = undef;
    for my $key (@{ $block->{'attrs'} }) {
      my $value = $key eq 'NODE_TEXT' ? wrap('  ', '  ', $item->string_value()) : $item->getAttribute($key);
      $value =~ s/\s+$//;

      $value ||= 'Unknown';

      $skip = 1
        if defined $block->{'filter'}{$key} &&
	  &{ $block->{'filter'}{$key} }($value);

      if (defined $block->{'rewrite'}{$key}) {
	my ($search, $replace);
	while (($search, $replace) = each %{ $block->{'rewrite'}{$key} } ) {
	  $value =~ s/$search/$replace/g;
	}
      }

      $template =~ s/__${key}__/$value/g;
    }
   
    print "$template\n"
      unless defined $skip;
  }

  print "\n";
}

=head1 NAME

mythtv-status - Display the status of a MythTV backend

=head1 SYNOPSIS

 mythtv-status [options]

=head1 DESCRIPTION

This script queries a MythTV backend and reports on the status of it,
any upcoming recordings and any which are happening right now.

The intention is to warn you if there is a program being recorded or
about to be recorded.

=head1 COMMAND LINE OPTIONS

=over

=item B<-d, --description>

Display the description for the scheduled recordings.

=item B<-e, --episode>

Display the episode (subtitle) for the scheduled recordings.

=item B<-h HOST, --host=HOST>

The host to check, defaults to localhost.

=item B<-p PORT, --port=PORT>

The port to use when connecting to MythTV, defaults to 6544.

=back

=head1 OUTPUT

The output of this script is broken up into several chunks they are:

=over

=item Status

Some general info about the backend, currently just the timestamp of then
this program was run.

=item Encoders

Each encoder that the backend knows about are listed, with the hostname
they are on, the encoder ID (in brackets) and the current status.

=item Recording Now

Any programs which are being recorded right now.

=item Scheduled Recordings

Up to 10 programs which are scheduled to be recorded today and tomorrow.

=back

=head1 AUTHOR

Andrew Ruthven, andrew@etc.gen.nz

=head1 LICENSE

Copyright (c) 2007 Andrew Ruthven <andrew@etc.gen.nz>
This code is hereby licensed for public consumption under the GNU GPL v2.

=cut

